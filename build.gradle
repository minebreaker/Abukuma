subprojects {
    group = 'rip.deadcode.abukuma3'
    version = '0.1.0-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'jacoco'

    repositories {
        mavenCentral()
        maven {
            url = "https://maven.pkg.github.com/minebreaker/Izvestia"
            credentials {
                username = project.hasProperty("GITHUB_USER") ? GITHUB_USER : ''
                password = project.hasProperty("GITHUB_TOKEN") ? GITHUB_TOKEN : ''
            }
        }
    }

    dependencies {
        testImplementation(
                'org.junit.jupiter:junit-jupiter-api:5.7.2',
                'com.google.truth:truth:1.1.3',
                'com.google.truth.extensions:truth-java8-extension:1.1.3',
                'org.mockito:mockito-core:3.12.4',
                'com.google.jimfs:jimfs:1.2',
                'rip.deadcode:izvestia:0.4.0',

                'org.slf4j:slf4j-api:1.7.32',
                'org.slf4j:jul-to-slf4j:1.7.32', // Guava uses java.util.logging
                'ch.qos.logback:logback-classic:1.2.5'
        )
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
    }

    javadoc {
        // Temporary suppress warnings
        options.addStringOption("Xdoclint:none", "-quiet")
        options.charSet = 'UTF-8'
        options.encoding = 'UTF-8'
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier.set('sources')
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier.set('javadoc')
        from javadoc.destinationDir
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    compileJava {
        options.encoding = 'UTF-8'
    }

    compileTestJava {
        options.encoding = 'UTF-8'
    }

    artifacts {
        archives jar
        archives sourcesJar
        archives javadocJar
    }

    test {
        useJUnitPlatform()
        systemProperties([
                "junit.jupiter.execution.parallel.enabled"     : true,
                "junit.jupiter.execution.parallel.mode.default": "concurrent"
        ])
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }

    check.dependsOn(jacocoTestReport)
}
