subprojects {
    group = 'rip.deadcode.abukuma3'
    version = '0.1.0-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'jacoco'

    repositories {
        mavenCentral()
        maven {
            url = "https://dl.bintray.com/minebreaker/test"
        }
    }

    dependencies {
        testImplementation(
                'org.junit.jupiter:junit-jupiter-api:5.6.2',
                'com.google.truth:truth:1.0.1',
                'com.google.truth.extensions:truth-java8-extension:1.0.1',
                'org.mockito:mockito-core:3.3.3',
//            'com.google.jimfs:jimfs:1.1',
                'rip.deadcode:izvestia:0.3',

                'org.slf4j:slf4j-api:1.7.30',
                'org.slf4j:jul-to-slf4j:1.7.30', // Guava uses java.util.logging
                'ch.qos.logback:logback-classic:1.2.3'
        )
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
    }

    javadoc {
        // Temporary suppress warnings
        options.addStringOption("Xdoclint:none", "-quiet")
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier.set('sources')
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier.set('javadoc')
        from javadoc.destinationDir
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    compileJava {
        options.encoding = 'UTF-8'
    }

    compileTestJava {
        options.encoding = 'UTF-8'
    }

    artifacts {
        archives jar
        archives sourcesJar
        archives javadocJar
    }

    test {
        useJUnitPlatform()
        systemProperties([
                "junit.jupiter.execution.parallel.enabled"     : true,
                "junit.jupiter.execution.parallel.mode.default": "concurrent"
        ])
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }

    check.dependsOn(jacocoTestReport)
}
