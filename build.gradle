subprojects {
    group = 'rip.deadcode.abukuma3'
    version = '0.1.0-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'jacoco'

    repositories {
        mavenCentral()
        maven {
            url = "https://dl.bintray.com/minebreaker/test"
        }
    }

    dependencies {
        testCompile(
                'org.junit.jupiter:junit-jupiter-api:5.3.2',
                'com.google.truth:truth:0.42',
                'com.google.truth.extensions:truth-java8-extension:0.42',
                'org.mockito:mockito-core:2.23.4',
//            'com.google.jimfs:jimfs:1.1',
                'rip.deadcode:izvestia:0.2',

                'org.slf4j:jul-to-slf4j:1.7.25', // Guava uses java.util.logging
                'ch.qos.logback:logback-classic:1.2.3'
        )
        testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.3.2'
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier.set('sources')
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier.set('javadoc')
        from javadoc.destinationDir
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    compileJava {
        options.encoding = 'UTF-8'
    }

    compileTestJava {
        options.encoding = 'UTF-8'
    }

    artifacts {
        archives jar
        archives sourcesJar
        archives javadocJar
    }

    test {
        useJUnitPlatform()
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }

    check.dependsOn(jacocoTestReport)
}
