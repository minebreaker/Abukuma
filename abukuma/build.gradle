import java.nio.charset.StandardCharsets
import java.nio.file.Files

dependencies {
    api(
            project(':onioko'),
            'javax.cache:cache-api:1.1.1',

            'com.google.guava:guava:31.1-jre',
            'org.slf4j:slf4j-api:1.7.33',
    )
}

sourceSets {
    main {
        java {
            srcDirs += "build/generated/sources/annotationProcessor/java/main"
        }
    }
}

def resourceDir = sourceSets.main.output.resourcesDir.toPath()

task writeCommitHash(type: Exec) {
    def commitFile = resourceDir.resolve("abukuma-commit")

    // This will disable incremental compile
//    doNotTrackState("")
    outputs.file(commitFile)

    def baos = new ByteArrayOutputStream()
    standardOutput = baos
    commandLine "git", "rev-parse", "HEAD"
    doLast {
        Files.createDirectories(resourceDir)
        Files.writeString(commitFile, baos.toString(StandardCharsets.UTF_8).trim())
    }
}

task writeVersion {
    def versionFile = resourceDir.resolve("abukuma-version")

//    doNotTrackState("")
    outputs.file(versionFile)

    doLast {
        Files.createDirectories(resourceDir)
        Files.writeString(versionFile, version)
    }
}
processResources.dependsOn(writeCommitHash, writeVersion)
//    compileTestKotlin.dependsOn(processResources)  // Gradle warns. Not sure why this is not default
